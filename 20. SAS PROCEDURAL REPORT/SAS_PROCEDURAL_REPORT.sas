/* PROC REPORT: */

/* SYNTAX--> */
/* PROC REPORT DATA=TABLE_NAME OUT=TABLE_NAME; */
/* COLUMNS<COLUM1><COLUM2><COLUM2>...    LIST ALL THE COLUMNS WHICH HAS TO BE USED IN PROC REPORT */
/* DEFINE COLUMN_NAME/ACTION;          DESCRIBE/ASSIGN ACTION TO EACH OF THE VARIABLES TO BE USED IN PROC REPORT */
/* BREAK COLUMN_NAME/ACTION;           SEPARATE ONE SEGMENT FROM ANOTHER OR SUMMARIZE BY SEGMENT, EG:- SUBTOTAL */
/* RBREAK COLUMN_NAME/ACTION;          SUMMARIZE AT THE BOTTOM, EG:- GRAND TOTAL */
/* COMPUTE VAR; */
/* 		LOGICS HERE;                   CONDITIONALLY CREATE NEW COLUMNS, DEFINE FORMAT OR OTHER ACTIONS */
/* ENDCOMP; */
/* RUN; */


/* EG-1: */

PROC REPORT DATA=SASHELP.CARS;
COLUMNS("COLUMNS I NEED" ORIGIN MAKE TYPE);
RUN;


/* EG-2: */
/* CATEGORIES BY COLUMNS */

PROC REPORT DATA=SASHELP.CARS;
COLUMNS("CARS DATA" ("CATEGORY" ORIGIN MAKE TYPE) ("MEASUR" MSRP INVOICE));
RUN;




/* EG-3: */
/* USE OF DEFINE STATEMENT */

/* SCENARIO-1: */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS("CARS DATA" ("CATEGORY" ORIGIN MAKE TYPE) ("MEASUR" MSRP INVOICE));
DEFINE ORIGIN/GROUP;
RUN;


/* SCENARIO-2: */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS("CARS DATA" ("CATEGORY" ORIGIN MAKE TYPE) ("MEASUR" MSRP INVOICE));
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/GROUP;
RUN;


/* SCENARIO-3: */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN TYPE;
DEFINE ORIGIN/GROUP;
DEFINE TYPE/GROUP;
RUN;


/* SCENARIO-4: */
/* COUNT OF TYPE BASE ON ORIGIN */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN TYPE;
DEFINE ORIGIN/GROUP;
DEFINE TYPE/ACROSS;
RUN;


/* SCENARIO-5.1: */
/* COUNT OF TYPE BASE ON ORIGIN */
/* BY DEFAULT IT WILL DO SUM */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN TYPE MSRP INVOICE;
DEFINE ORIGIN/GROUP;
DEFINE TYPE/ACROSS;
RUN;

/* SCENARIO-5.2: */
/* COUNT OF TYPE BASE ON ORIGIN */
/* BY DEFAULT IT WILL DO SUM */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE TYPE/ACROSS;
RUN;

/* SCENARIO-6: */
/* MEAN OF MSRP */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
RUN;

/* SCENARIO-6.1: */
/* MEAN OF MSRP */
/* TOTAL OF INVOICE */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
RUN;



/* EG-4: */
/* USING WHERE CLAUSE FOR CONDITION/FILTER */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN MAKE TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
WHERE UPCASE(ORIGIN) IN ("ASIA","EUROPE");
RUN;


/* EG-5: */
/* USING BREAK STATEMENT */
/* IT WILL SHOW TOTAL IN THE LAST OF THE GROUP. */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN MAKE TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
BREAK AFTER ORIGIN/SUMMARIZE;
WHERE UPCASE(ORIGIN) IN ("ASIA","EUROPE");
RUN;


/* EG-5.1: */
/* USING BREAK STATEMENT */
/* IF YOU WANT TO SHOW THE ORGINS IN DIFFERENT PAGES. */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN MAKE TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
BREAK AFTER ORIGIN/PAGE;

WHERE UPCASE(ORIGIN) IN ("ASIA","EUROPE");
RUN;




/* EG-6: */
/* USING RBREAK STATEMENT */
/* IT WILL SHOW GRAND TOTAL IN THE LAST OF TABLE. */
PROC REPORT DATA=SASHELP.CARS;
COLUMNS ORIGIN MAKE TYPE,(MSRP INVOICE);
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
BREAK AFTER ORIGIN/SUMMARIZE;
RBREAK AFTER/SUMMARIZE;
WHERE UPCASE(ORIGIN) IN ("ASIA","EUROPE");
RUN;



/* EG-7: */
/* USING OUT STATEMENT */
/* IF YOU WANT TO STORE INTO ANOTHER TABLE. */
PROC REPORT DATA=SASHELP.CARS OUT=CARSS;
COLUMNS ORIGIN MAKE TYPE,(MSRP INVOICE) DIFF;
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
BREAK AFTER ORIGIN/SUMMARIZE;

WHERE UPCASE(ORIGIN) IN ("ASIA","EUROPE");
RUN;




/* EG-8: */
/* USING COMPUTE STATEMENT */
/* IF YOU SHOW DIFFERENCE BETWEEN TWO COLUMNS. */
PROC REPORT DATA=SASHELP.CARS OUT=CARSS;
COLUMNS ORIGIN MAKE TYPE,(MSRP INVOICE) DIFF;
DEFINE ORIGIN/GROUP;
DEFINE MAKE/GROUP;
DEFINE TYPE/ACROSS;
DEFINE MSRP/"AVG_MSRP" MEAN;
DEFINE INVOICE/"TOTAL_INVOICE" SUM;
DEFINE DIFF/COMPUTED;
BREAK AFTER ORIGIN/SUMMARIZE;

COMPUTE DIFF;
	DIFF=_C3_ - _C4_;
ENDCOMP;

WHERE UPCASE(ORIGIN) IN ("ASIA","EUROPE");
RUN;












